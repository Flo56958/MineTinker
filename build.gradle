import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    // Spigot
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://repo.codemc.org/repository/maven-public')
    }

    // ProtocolLib
    maven {
        url = uri("https://repo.dmulloy2.net/repository/public/")
    }

}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0"
}

group = 'de.flo56958'
version = '2.0'
description = 'A TinkersConstruct (and other mods) inspired spigot server plugin'
java.sourceCompatibility = JavaVersion.VERSION_20 // TODO: Switch to Java 21 when shadow supports it

jar.dependsOn(processResources)
assemble.dependsOn(shadowJar)

shadowJar {
    relocate('org.bstats', 'de.flo56958.minetinker.bStats')
    archiveFileName = "MineTinker.jar"
}

compileJava {
	options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation'
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "project.version"    : project.version,
            "project.url"        : "https://github.com/Flo56958/MineTinker",
            "project.description": project.description
    ]
}
