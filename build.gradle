import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
    id 'net.ltgt.errorprone' version '4.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    // Spigot
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://repo.codemc.org/repository/maven-public')
    }

    // ProtocolLib
    maven {
        url = uri("https://repo.dmulloy2.net/repository/public/")
    }

}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'

    compileOnly 'org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.1.0'

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0"

    errorprone("com.google.errorprone:error_prone_core:2.36.0")
}

group = 'de.flo56958'
version = '2.0'
description = 'A TinkersConstruct (and other mods) inspired spigot server plugin'
java.sourceCompatibility = JavaVersion.VERSION_21

jar.dependsOn(processResources)
assemble.dependsOn(shadowJar)

shadowJar {
    relocate('org.bstats', 'de.flo56958.minetinker.bStats')
    archiveFileName = "MineTinker.jar"
}

compileJava {
	options.encoding = 'UTF-8'

    options.deprecation = true

    options.errorprone.disable('MissingSummary')
    options.errorprone.error(
            'NonCanonicalType', 'JdkObsolete', 'MissingOverride',
            'UnnecessaryParentheses', 'OperatorPrecedence')
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "project.version"    : project.version,
            "project.url"        : "https://github.com/Flo56958/MineTinker",
            "project.description": project.description
    ]
}
